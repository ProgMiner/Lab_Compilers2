-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)
--      | Seq     (expr, expr)
--      | Skip
--      | Read    (string)
--      | Write   (expr)
--      | If      (expr, expr, expr)
--      | While   (expr, expr)
--      | DoWhile (expr, expr)
--      | Var     (string)
--      | Ref     (string)
--      | Const   (int)
--      | Binop   (string, expr, expr)
--      | Scope   (def list, expr)
--      | Call    (string, expr list)
--      | Ignore  (expr)
--
-- A definition is either a local variable definition or a function
-- definition:
--
-- def = Var (string)
--     | Fun (string, string list, expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (x) -> x
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    f@Fun (_, _) -> f
  | _            -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

(* Assignment *)
fun void (c) { [c, Z] }

fun eval (c@[s, w], expr) {
  case expr of
    Assn (v, x) -> case evalList (c, {v, x}) of
      [[s, w], {v, x}] -> [[s <- [v, Var (x)], w], x]
    esac
  | Seq (l, r) -> case evalList (c, {l, r}) of
      [c, {_, x}] -> [c, x]
    esac
  | Skip -> void (c)
  | Read (v) -> case w.readWorld of
      [x, w] -> void ([s <- [v, Var (x)], w])
    esac
  | Write (x) -> case eval (c, x) of
      [c, x] -> void ([s, writeWorld (x, w)])
    esac
  | If (cond, t, f) -> case eval (c, cond) of
      [c, cond] -> if cond then eval (c, t) else eval (c, f) fi
    esac
  | While (cond, b) -> case eval (c, cond) of
      [c, cond] -> if cond then eval (eval (c, b) [0], expr) else void (c) fi
    esac
  | DoWhile (b, cond) -> case eval (eval (c, b) [0], cond) of
      [c, cond] -> if cond then eval (c, expr) else void (c) fi
    esac
  | Var   (v) -> [c, s.checkVar (v)]
  | Ref   (v) -> s.checkVar (v); [c, v]
  | Const (x) -> [c, x]
  | Binop (op, l, r) -> case evalList (c, {l, r}) of
      [c, {l, r}] -> [c, evalOp (op, l, r)]
    esac
  | Scope (ds, e) ->
      case foldl (fun (s, d) {
        case d of
          Var (x) -> s.addName (x, Var (0))
        | Fun (f, xs, e) -> s.addFunction (f, xs, e)
        esac
      }, s.enterScope, ds) of
        s -> case eval ([s, w], e) of
          [[s, w], res] -> [[s.leaveScope, w], res]
        esac
      esac
  | Call (f, es) -> case s.checkFun (f) of
      Fun (xs, e) -> case evalList (c, es) of
        [[s, w], vs] -> case foldl (fun (s, [x, v]) {
          s.addName (x, Var (v))
        }, s.enterFunction, zip (xs, vs)) of
          sLocal -> case eval ([sLocal, w], e) of
            [[sLocal, w], res] -> [[s.leaveFunction (sLocal.getGlobal), w], res]
          esac
        esac
      esac
    esac
  | Ignore (x) -> case eval (c, x) of
      [c, _] -> void (c)
    esac
  | _ -> failure (sprintf ("evalExpr not implemented: %s\n", expr.string))
  esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
