-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval ([st, w], stmt) {
  case stmt of
    Assn  (x, v) -> [st <- [x, evalExpr (st, v)], w]
  | Seq   (a, b) -> eval (eval ([st, w], a), b)
  | Skip         -> [st, w]
  | Read  (x)    -> case readWorld (w) of [v, w] -> [st <- [x, v], w] esac
  | Write (x)    -> [st, writeWorld (evalExpr (st, x), w)]
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
